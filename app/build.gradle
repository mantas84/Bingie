apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def apikeyPropertiesFile = rootProject.file('apikey.properties')
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

def storePropertiesFile = rootProject.file('keystore.properties')
def storeProperties = new Properties()
storeProperties.load(new FileInputStream(storePropertiesFile))

kapt {
    correctErrorTypes = true
}

android {
    signingConfigs {
        release {
            keyAlias storeProperties['keyAlias']
            keyPassword storeProperties['keyPassword']
            storeFile file(storeProperties['storeFile'])
            storePassword storeProperties['storePassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    defaultConfig {
        applicationId "eu.oncreate.bingie"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            buildConfigField("String", "TRAKT_CLIENT_ID", apikeyProperties['TRAKT_CLIENT_ID'])
            buildConfigField("String", "TMDB_TOKEN", apikeyProperties['TMDB_TOKEN'])
            buildConfigField("String", "TVDB_KEY", apikeyProperties['TVDB_KEY'])
            buildConfigField("String", "FANART_KEY", apikeyProperties['FANART_KEY'])
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField("String", "TRAKT_CLIENT_ID", apikeyProperties['TRAKT_CLIENT_ID_DEV'])
            buildConfigField("String", "TMDB_TOKEN", apikeyProperties['TMDB_TOKEN'])
            buildConfigField("String", "TVDB_KEY", apikeyProperties['TVDB_KEY'])
            buildConfigField("String", "FANART_KEY", apikeyProperties['FANART_KEY'])
            minifyEnabled false
            applicationIdSuffix = ".debug"
            versionNameSuffix "-debug"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"

    // Dagger2
    implementation "com.google.dagger:dagger:2.22.1"
    kapt "com.google.dagger:dagger-compiler:2.22.1"
    implementation "com.google.dagger:dagger-android-support:2.22.1"
    kapt "com.google.dagger:dagger-android-processor:2.22.1"
    implementation 'com.squareup.inject:assisted-inject-annotations-dagger2:0.4.0'
    kapt 'com.squareup.inject:assisted-inject-processor-dagger2:0.4.0'

    implementation "com.squareup.moshi:moshi:1.8.0"
    implementation "com.squareup.moshi:moshi-kotlin:1.8.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.8.0"
    implementation "com.squareup.moshi:moshi-adapters:1.8.0"

    implementation "com.squareup.retrofit2:retrofit:2.8.1"
    implementation "com.squareup.retrofit2:converter-moshi:2.8.1"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.8.1"
    implementation "com.squareup.okhttp3:logging-interceptor:4.2.1"

    implementation "com.jakewharton.timber:timber:4.7.1"

    implementation "com.github.bumptech.glide:glide:4.11.0"
    kapt "com.github.bumptech.glide:compiler:4.11.0"

    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    implementation "androidx.navigation:navigation-fragment-ktx:2.2.2"
    implementation "androidx.navigation:navigation-ui-ktx:2.2.2"

    implementation "com.google.android.material:material:1.3.0-alpha01"

    implementation 'com.airbnb.android:mvrx:1.3.0'

    implementation 'com.airbnb.android:epoxy:3.10.0'
    kapt 'com.airbnb.android:epoxy-processor:3.10.0'

    implementation 'com.github.sephiroth74:NumberSlidingPicker:1.0.3'
    implementation 'com.robinhood.ticker:ticker:2.0.2'

    implementation "androidx.room:room-runtime:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    implementation "androidx.room:room-rxjava2:2.2.5"
    testImplementation "androidx.room:room-testing:2.2.5"
}
